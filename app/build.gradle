plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "com.google.protobuf" version "0.8.17"
    id "kotlin-parcelize"
}


ext.compose_version = '1.2.0'
ext.versionMajor = 0
ext.versionMinor = 0
ext.versionPatch = 1
ext.versionClassifier = null
ext.isSnapshot = true

def keystorePropertiesFile = rootProject.file("keystore.properties")
def endpointPropertiesFile = file('endpoint.properties')
def keystoreProperties = new Properties()
def endpointProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
endpointProperties.load(new FileInputStream(endpointPropertiesFile))

android {
    compileSdk 32

    signingConfigs {
        generalConfig {
            keyAlias = keystoreProperties["keyAlias"]
            keyPassword = keystoreProperties["keyPassword"]
            storeFile = file(keystoreProperties["storeFile"])
            storePassword = keystoreProperties["storePassword"]
        }
    }

    defaultConfig {
        applicationId "com.android.borsappc"
        minSdk 21
        targetSdk 32
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.generalConfig
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled true
            // Use minifyEnabled for activating R* only, increase compose build time presumably
//            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"

    productFlavors {
        dev {
            manifestPlaceholders = [appName: "@string/app_name"]
            dimension "env"
            versionNameSuffix "-dev"
            buildConfigField 'String', 'API_URL', endpointProperties['DEV_URL']
        }
        prod {
            manifestPlaceholders = [appName: "@string/app_name"]
            dimension "env"
            buildConfigField 'String', 'API_URL', endpointProperties['PROD_URL']
        }
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    buildToolsVersion '30.0.3'
    namespace 'com.android.borsappc'
}

private Integer generateVersionCode() {
	return versionMajor * 10000 + versionMinor * 100 + versionPatch
}

private String generateVersionName() {
	String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
	if (versionClassifier == null && isSnapshot) {
		versionClassifier = "SNAPSHOT"
	}

	if (versionClassifier != null) {
		versionName += "-" + versionClassifier
	}
	return versionName
}

protobuf {
    // Configures the Protobuf compilation and the protoc executable
    protoc {
        // Downloads from the repositories
        artifact = "com.google.protobuf:protoc:3.14.0"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project
    generateProtoTasks {
        all().each { task ->
            task.builtins {
            // Configures the task output type
                java {
            // Java Lite has smaller code size and is recommended for Android
                    option 'lite'
                }
            }
        }
    }
 }

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
//    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //==================== Lifecycle ====================
    def lifecycle_version = "2.6.0-alpha01"
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // ViewModel utilities for Compose
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    // LiveData
//    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // Lifecycles only (without ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    // Annotation processor
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    // alternately - if using Java8, use the following instead of lifecycle-compiler
//    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    //==================== Compose ====================
    implementation "androidx.compose.ui:ui:$compose_version"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation 'androidx.compose.foundation:foundation:1.3.0-alpha02'
    // Material Design
    implementation "androidx.compose.material:material:$compose_version"
    // Material design icons
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.5.1'
    // Integration with ViewModels
//    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.0'
    // Integration with observables
//    implementation 'androidx.compose.runtime:runtime-livedata:1.1.1'
//    implementation 'androidx.compose.runtime:runtime-rxjava2:1.1.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    //==================== Dependency Injection ====================
    def hilt_version = "2.42"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    //==================== Data Store ====================
    implementation  "androidx.datastore:datastore:1.0.0"
    // Starting from Protobuf 3.8.0, use the lite runtime library
    implementation  "com.google.protobuf:protobuf-javalite:3.18.0"

    //==================== Logging ====================
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //==================== Networking ====================
    def retrofit_version = '2.9.0'
    def okhttp_version = '4.10.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    //==================== Database ====================
    def room_version = '2.4.3'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    //==================== Navigation ====================
    implementation "androidx.navigation:navigation-compose:2.5.1"

    //==================== Memory Leak Detection ====================
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
}